buildscript {
    ext {
        springBootVersion = '1.1.2.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot' 

jar {
    baseName = 'sheriff'
    version =  '0.0.1-SNAPSHOT'
}
sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenCentral()
    mavenLocal()
}
sourceSets {
    integrationTests {
        java.srcDir file('src/integration/java')
        resources.srcDir file('src/integration/resources')
    }
}

task integrationTests(type: Test) {
    testClassesDir = sourceSets.integrationTests.output.classesDir
    classpath = sourceSets.integrationTests.runtimeClasspath
    exclude 'com/bitcoinj/wallet/**'
}

check.dependsOn integrationTests

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.hateoas:spring-hateoas")
    compile('com.h2database:h2:1.4.179')
    compile 'com.google:bitcoinj:0.12-SNAPSHOT'
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile('com.jayway.jsonpath:json-path:0.9.1')
    testCompile('com.jayway.jsonpath:json-path-assert:0.9.1')
    integrationTestsCompile sourceSets.main.output
    integrationTestsCompile configurations.testCompile
    integrationTestsCompile sourceSets.test.output
    integrationTestsRuntime configurations.testRuntime
}

eclipse {
    classpath {
         containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
         containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.7'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.12'
}
